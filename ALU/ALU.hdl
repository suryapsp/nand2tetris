// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=notx);
    Not16(in=y, out=noty);

// ZX and ZY flags handling
    Mux16(a=x, b=false, sel=zx, out=zxout);
    Mux16(a=y, b=false, sel=zy, out=zyout);

// NX and NY flag handling
    Not16(in=zxout, out=notzxout);
    Not16(in=zyout, out=notzyout);

    Mux16(a=zxout, b=notzxout, sel=nx, out=readyx);
    Mux16(a=zyout, b=notzyout, sel=ny, out=readyy);

// f control bit handling
    And16(a=readyx, b=readyy, out=xandy);
    Add16(a=readyx, b=readyy, out=xaddy);
    
    Mux16(a=xandy, b=xaddy, sel=f, out=readyout);
    
// NO flag handling, out, ng flag
    Not16(in=readyout, out=notreadyout);
    
    Mux16(a=readyout, b=notreadyout, sel=no, out=out, out[15]=ng, out[0..7]=zrLow, out[8..15]=zrHigh);
    
// ZR flag handling
    Or8Way(in=zrLow, out=zr1);
    Or8Way(in=zrHigh, out=zr2);
    Or(a=zr1, b=zr2, out=finalzr);
    Not(in=finalzr, out=zr);
}